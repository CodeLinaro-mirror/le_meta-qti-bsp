FILESEXTRAPATHS:prepend := "${THISDIR}/${PN}-${PV}:"

DEPENDS += "base-passwd"

SRC_URI:append = " \
    file://fstab \
    ${@bb.utils.contains('DISTRO_FEATURES', 'selinux', ' file://sh_login', '', d)} \
"

SRC_URI:append:gvm-gen4-5 = " \
    file://hgy/fstab \
    file://hqx/fstab \
"

dirs755:append = " \
    /media/cf /media/net /media/ram \
    /media/union /media/realroot /media/hdd /media/mmc1 \
    /firmware /dsp /bluetooth ${localstatedir} /media/card /persist \
    ${userfsdatadir} ${MACHINE_MNT_POINTS} \
"

do_install:append(){
    if(${@bb.utils.contains('MACHINE_FEATURES', 'qti-umd', 'true', 'false', d)}); then
        ln -s ${nonarch_base_libdir} ${D}/lib64
        ln -s ${libdir} ${D}/usr/lib64
    fi

    if(${@bb.utils.contains('MACHINE_FEATURES', 'early-ramdisk-init', 'true', 'false', d)}); then
        install -d ${D}/boot/early-ramdisk
    fi

    install -m 0644 ${WORKDIR}/fstab ${D}${sysconfdir}/fstab
    # Install login wrapper to enable user login for busybox sh
    if ${@bb.utils.contains('DISTRO_FEATURES', 'selinux', 'true', 'false', d)}; then
        install -m 0755 ${WORKDIR}/sh_login ${D}${base_bindir}/sh_login
    fi


    # Replace persist/home bind if read-only is not enabled
    if ${@bb.utils.contains('IMAGE_FEATURES', 'read-only-rootfs', 'false', 'true', d)}; then
        sed -i "/^\PARTLABEL=persist.*var/d" ${D}${sysconfdir}/fstab
        sed -i "/^\#.*Bind/d" ${D}${sysconfdir}/fstab
        sed -i "/^\/data/d" ${D}${sysconfdir}/fstab
        sed -i "/^\${localstatedir}/d" ${D}${sysconfdir}/fstab
    fi

    if ${@bb.utils.contains('DISTRO_FEATURES', 'volatiled-var', 'true', 'false', d)}; then
        sed -i "/\/var\/volatile/s/\/volatile/         /" ${D}${sysconfdir}/fstab
        sed -i '/^\/dev\/vdc/ s/var/persist/' ${D}${sysconfdir}/fstab
        sed -i "/^\${localstatedir}/d" ${D}${sysconfdir}/fstab
    fi
}

do_install:append:gvm-gen4-5() {
    install -d ${D}/uni/hqx/etc
    install -d ${D}/uni/hgy/etc
    install -m 0644 ${WORKDIR}/hqx/fstab ${D}/uni/hqx/etc/fstab
    install -m 0644 ${WORKDIR}/hgy/fstab ${D}/uni/hgy/etc/fstab

    # Replace persist/home bind if read-only is not enabled
    if ${@bb.utils.contains('IMAGE_FEATURES', 'read-only-rootfs', 'false', 'true', d)}; then
        sed -i "/^\PARTLABEL=persist.*var/d" ${D}/uni/hqx/etc/fstab
        sed -i "/^\PARTLABEL=persist.*var/d" ${D}/uni/hgy/etc/fstab
        sed -i "/^\#.*Bind/d" ${D}/uni/hqx/etc/fstab
        sed -i "/^\#.*Bind/d" ${D}/uni/hgy/etc/fstab
        sed -i "/^\/data/d" ${D}/uni/hqx/etc/fstab
        sed -i "/^\/data/d" ${D}/uni/hgy/etc/fstab
        sed -i "/^\${localstatedir}/d" ${D}/uni/hqx/etc/fstab
        sed -i "/^\${localstatedir}/d" ${D}/uni/hgy/etc/fstab
    fi
}
