SUMMARY = "adbd-linux provides adbd-relay service"

DESCRIPTION = "adbd-linux is a fork of https://github.com/tonyho/adbd-linux \
which is a port of adb to Linux. This fork extends tonyho's \
adbd-linux with adbd-relay which can be used to proxy adb from one VM to another."

HOMEPAGE = "https://git.codelinaro.org"

LICENSE = "Apache-2.0"
LIC_FILES_CHKSUM = "file://NOTICE;md5=c1a3ff0b97f199c7ebcfdd4d3fed238e"

FILESPATH =+ "${WORKSPACE}:"
SRC_URI = "\
    file://vendor/qcom/opensource/coqos-adbd \
    file://adbd-relay.service \
"

SRCREV = "${AUTOREV}"

S = "${WORKDIR}/vendor/qcom/opensource/coqos-adbd"

inherit pkgconfig systemd

DEPENDS = "openssl libcap glib-2.0 systemd"

SYSTEMD_PACKAGES = "adbd-relay"
SYSTEMD_SERVICE:adbd-relay = "adbd-relay.service"

# Source files are not included in the packages
PACKAGE_DEBUG_SPLIT_STYLE = "debug-without-src"

PACKAGES = " \
    adbd-relay-dbg \
    adbd-relay \
"

FILES:adbd-relay = " \
    ${sbindir}/adbd-relay \
    ${systemd_system_unitdir}/adbd-relay.service \
"

FILES:adbd-relay-dbg = " \
    ${sbindir}/.debug \
"

# Avoid QA Issue: No GNU_HASH in the elf binary
INSANE_SKIP:adbd-relay = "ldflags"
INSANE_SKIP:adbd-relay-dbg += "buildpaths"

do_configure() {
    ./configure ${EXTRA_OECONF}
}

do_compile() {
    oe_runmake all
}

do_install() {
    oe_runmake install 'DESTDIR=${D}'

    # There is no separate install target for adbd-relay.
    # Remove all unwanted byproducts of the build/install process.
    rm -f ${D}${sbindir}/xdg-adbd
    rm -f ${D}${sbindir}/adbd
    # adb-usb2tcp is a host tool
    rm -f ${D}${sbindir}/adb-usb2tcp

    # Avoid installation of adbd systemd service file since the version provided
    # by adbd-linux repository can not satisfy all possible use cases.
    rm -rf ${D}/usr/etc/systemd
    rmdir --ignore-fail-on-non-empty ${D}/usr/etc 2>/dev/null || true

    # install locally provided systemd unit file
    install -d ${D}${systemd_system_unitdir}
    install -m 0644 ${WORKDIR}/adbd-relay.service ${D}${systemd_system_unitdir}
}
